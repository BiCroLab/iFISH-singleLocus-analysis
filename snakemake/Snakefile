try:
    import pygpseq
except ImportError as e:
    raise Exception("Missing `pygpseq`. Please install it with `pip3 install pygpseq`.")

TIFF_FINDOOF_VERSION = subprocess.check_output("tiff_findoof --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
TIFF_AUTO3DSEG_VERSION = subprocess.check_output("tiff_auto3dseg --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
GPSEQ_FROMFISH_VERSION = subprocess.check_output("gpseq_fromfish --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
GPSEQ_FROMFISH_MERGE_VERSION = subprocess.check_output("gpseq_fromfish_merge --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
RM_FAILED_CHANNELS_VERSION = subprocess.check_output("../scripts/rmFailedChannels.py --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
NUCLEIDS_STATS_VERSION = subprocess.check_output("../scripts/nucleiDatasetStats.R --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
NUCLEI_POPSEL_VERSION = subprocess.check_output("../scripts/nucleiSelectPopulation.R --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")

ORF = config['output_root_folder']

rule all:
    input:
        "%s/fromfish_merged/dots.merged.noDiscardedChannels.G1selected.tsv" % ORF,

rule Find_out_of_focus:
    version: TIFF_FINDOOF_VERSION
    input:
        lambda wildcards: expand("%s/%s/" % (config['deconvolved_root_folder'], wildcards.sample))
    output:
        "%s/oof/{sample}.pdf" % ORF
    params:
        imdir = "%s/{sample}" % config['deconvolved_root_folder'],
        outdir = "%s/oof" % ORF,
        output = "%s/oof/{sample}.tsv" % ORF
    threads: 1
    shell: """
        mkdir -p {params.outdir}
        tiff_findoof -PR {params.imdir} {params.output}
    """

rule Automatic_3D_segmentation:
    version: TIFF_AUTO3DSEG_VERSION
    input:
        lambda wildcards: expand("%s/oof/%s.pdf" % (ORF, wildcards.sample))
    output:
        directory("%s/{sample}/" % config['masks_root_folder'])
    log:
        "%s/{sample}/snakemake.log" % config['masks_root_folder']
    params:
        imdir = "%s/{sample}/" % config['deconvolved_root_folder'],
        outdir = "%s/{sample}/" % config['masks_root_folder'],
        compressed = "--compressed" if config['mask_compressed'] else "",
        labeled = "--labeled" if config['mask_labeled'] else ""
    threads: config['threads_per_job']
    shell: """
        mkdir -p {params.outdir}
        tiff_auto3dseg -t {threads} {params.compressed} {params.labeled} {params.imdir} {params.outdir} &> {log}
    """

rule Calculate_Lamina_Distance:
    version: GPSEQ_FROMFISH_VERSION
    input:
        lambda wildcards: expand([
                "%s/%s/" % (config['masks_root_folder'], wildcards.sample),
                "%s/%s_auto_userdots.csv" % (config['auto_userdots_folder'], wildcards.sample),
                "%s/%s/" % (config['deconvolved_root_folder'], wildcards.sample)
            ])
    output:
        "%s/fromfish/{sample}/wCentr.out.dilate%d.{sample}_auto_userdots.tsv" % (ORF, config['dilation'])
    log:
        "%s/fromfish/{sample}/snakemake.log" % ORF
    params:
        dot_coords = "%s/{sample}_auto_userdots.csv" % config['auto_userdots_folder'],
        imdir = "%s/{sample}/" % (config['deconvolved_root_folder']),
        outdir = "%s/fromfish/{sample}/" % ORF,
        dilation = config['dilation'],
        aspect = " ".join([str(x) for x in config['aspect']]),
        distance_type = config['distance_type'],
        mask_params = "-m '%s/{sample}/' -M '%s'" % (config["masks_root_folder"], config['mask_prefix']),
        compressed = "--compressed" if config['mask_compressed'] else "",
        labeled = "--labeled" if config['mask_labeled'] else "",
        axis_fraction = config['axis_fraction'],
        discard_channels = " ".join(config['discard_channels']),
        dilate_Z = "--dilate-Z" if config['dilate_Z'] else ""
    threads: config['threads_per_job']
    shell: """
        mkdir -p {params.outdir}
        gpseq_fromfish -a {params.aspect} -D {params.dilation} --dist-type {params.distance_type} -t {threads} \
            {params.mask_params} {params.compressed} {params.labeled} -P {params.axis_fraction} -C {params.discard_channels} \
            {params.dilate_Z} '{params.dot_coords}' '{params.imdir}' '{params.outdir}' -y &> {log}
    """

rule Merge_datasets:
    version: GPSEQ_FROMFISH_MERGE_VERSION
    input:
        expand("%s/fromfish/{sample}/wCentr.out.dilate%d.{sample}_auto_userdots.tsv" % (ORF, config['dilation']), sample = config['datasets'])
    output:
        "%s/fromfish_merged/copies.merged.tsv" % ORF,
        "%s/fromfish_merged/density_profile.merged.tsv" % ORF,
        "%s/fromfish_merged/dots.merged.tsv" % ORF,
        "%s/fromfish_merged/ncomps.merged.tsv" % ORF,
        "%s/fromfish_merged/nuclei.merged.tsv" % ORF,
        "%s/fromfish_merged/volume_profile.merged.tsv" % ORF
    log:
        "%s/fromfish_merged/gpseq_fromfish_merge.log" % ORF
    params:
        meta = config['metadata'],
        indir = "%s/fromfish" % ORF,
        outdir = "%s/fromfish_merged" % ORF,
        aspect = " ".join([str(x) for x in config['aspect']]),
        sep = "-d %s" % config['metadata_delim'] if "\t" != config['metadata_delim'] else ""
    threads: 100
    shell: """
        mkdir -p {params.outdir}
        gpseq_fromfish_merge --no-date -t {threads} \
            -m {params.meta} -i {params.indir} -o {params.outdir} \
            -a {params.aspect} {params.sep} &> {log}
    """

rule Check_nuclei:
    version: NUCLEIDS_STATS_VERSION
    output:
        "%s/fromfish_merged/stats/nuclei_by_dataset.{suffix}.pdf" % ORF
    log:
        "%s/fromfish_merged/log/nuclei_dataset_stats.{suffix}.log" % ORF
    params:
        indir = "%s/fromfish_merged" % ORF
    threads: 1
    shell: """
        mkdir -p {params.indir}/stats
        ../scripts/nucleiDatasetStats.R {params.indir}/nuclei.{suffix}.tsv \
            {params.indir}/ncomps.{suffix}.tsv {params.indir}/stats \
            -s {suffix} &> {log}
    """

rule Remove_failed_channels:
    version: RM_FAILED_CHANNELS_VERSION
    input:
        "%s/fromfish_merged/dots.merged.tsv" % ORF,
        "%s/fromfish_merged/stats/nuclei_by_dataset.merged.pdf" % ORF # Sanity check
    output:
        "%s/fromfish_merged/dots.merged.noDiscardedChannels.tsv" % ORF
    log:
        "%s/fromfish_merged/log/discarded_channel_removal.log" % ORF
    params:
        indir = "%s/fromfish_merged" % ORF
    threads: 1
    shell: """
        mkdir -p {params.indir}/log
        ../scripts/rmFailedChannels.py {params.indir} &> {log}
    """

rule Select_nuclear_G1_population:
    version: NUCLEI_POPSEL_VERSION
    input:
        "%s/fromfish_merged/dots.merged.noDiscardedChannels.tsv" % ORF
    output:
        "%s/fromfish_merged/dots.merged.noDiscardedChannels.G1selected.tsv" % ORF,
        "%s/fromfish_merged/nuclei.merged.G1selected.tsv" % ORF,
        "%s/fromfish_merged/ncomps.merged.G1selected.tsv" % ORF
    log:
        "%s/fromfish_merged/log/nuclei_population_selection.log" % ORF
    params:
        indir = "%s/fromfish_merged" % ORF,
        ksigma = config['k_sigma_population_selection']
    threads: 100
    shell: """
        ../scripts/nucleiSelectPopulation.R \
            {params.indir}/dots.merged.noDiscardedChannels.tsv \
            {params.indir}/nuclei.merged.tsv \
            {params.indir}/ncomps.merged.tsv \
            {params.indir}/stats \
            -t {threads} --ksigma {params.ksigma} &> {log}
    """
