try:
    import pygpseq
except ImportError as e:
    raise Exception("Missing `pygpseq`. Please install it with `pip3 install pygpseq`.")

TIFF_FINDOOF_VERSION = subprocess.check_output("tiff_findoof --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
TIFF_AUTO3DSEG_VERSION = subprocess.check_output("tiff_auto3dseg --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
GPSEQ_FROMFISH_VERSION = subprocess.check_output("gpseq_fromfish --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")
GPSEQ_FROMFISH_MERGE_VERSION = subprocess.check_output("gpseq_fromfish_merge --version", shell = True).decode("utf-8").strip().split(" ")[1].strip("v")

rule all:
    input:
        "%s/fromfish_merged/dots.merged.tsv" % config['output_root_folder']

rule OOF_Detection:
    version: TIFF_FINDOOF_VERSION
    input:
        lambda wildcards: expand("%s/%s/" % (config['deconvolved_root_folder'], wildcards.sample))
    output:
        "%s/oof/{sample}.pdf" % config['output_root_folder']
    params:
        imdir = "%s/{sample}" % config['deconvolved_root_folder'],
        outdir = "%s/oof" % config['output_root_folder'],
        output = "%s/oof/{sample}.tsv" % config['output_root_folder']
    threads: 1
    shell: """
        mkdir -p {params.outdir}
        tiff_findoof -PR {params.imdir} {params.output}
    """


rule Automatic_3D_segmentation:
    version: TIFF_AUTO3DSEG_VERSION
    input:
        lambda wildcards: expand("%s/oof/%s.pdf" % (config['output_root_folder'], wildcards.sample))
    output:
        directory("%s/{sample}/" % config['masks_root_folder'])
    log:
        "%s/{sample}/snakemake.log" % config['masks_root_folder']
    params:
        imdir = "%s/{sample}/" % config['deconvolved_root_folder'],
        outdir = "%s/{sample}/" % config['masks_root_folder'],
        compressed = "--compressed" if config['mask_compressed'] else "",
        labeled = "--labeled" if config['mask_labeled'] else ""
    threads: config['threads_per_job']
    shell: """
        mkdir -p {params.outdir}
        tiff_auto3dseg -t {threads} {params.compressed} {params.labeled} {params.imdir} {params.outdir} &> {log}
    """

rule Lamina_distance_calculation:
    version: GPSEQ_FROMFISH_VERSION
    input:
        lambda wildcards: expand([
                "%s/%s/" % (config['masks_root_folder'], wildcards.sample),
                "%s/%s_auto_userdots.csv" % (config['auto_userdots_folder'], wildcards.sample),
                "%s/%s/" % (config['deconvolved_root_folder'], wildcards.sample)
            ])
    output:
        "%s/fromfish/{sample}/wCentr.out.dilate%d.{sample}_auto_userdots.tsv" % (config['output_root_folder'], config['dilation'])
    log:
        "%s/fromfish/{sample}/snakemake.log" % config['output_root_folder']
    params:
        dot_coords = "%s/{sample}_auto_userdots.csv" % config['auto_userdots_folder'],
        imdir = "%s/{sample}/" % (config['deconvolved_root_folder']),
        outdir = "%s/fromfish/{sample}/" % config['output_root_folder'],
        dilation = config['dilation'],
        aspect = " ".join([str(x) for x in config['aspect']]),
        distance_type = config['distance_type'],
        mask_params = "-m '%s/{sample}/' -M '%s'" % (config["masks_root_folder"], config['mask_prefix']),
        compressed = "--compressed" if config['mask_compressed'] else "",
        labeled = "--labeled" if config['mask_labeled'] else "",
        axis_fraction = config['axis_fraction'],
        discard_channels = " ".join(config['discard_channels']),
        dilate_Z = "--dilate-Z" if config['dilate_Z'] else ""
    threads: config['threads_per_job']
    shell: """
        mkdir -p {params.outdir}
        gpseq_fromfish -a {params.aspect} -D {params.dilation} --dist-type {params.distance_type} -t {threads} \
            {params.mask_params} {params.compressed} {params.labeled} -P {params.axis_fraction} -C {params.discard_channels} \
            {params.dilate_Z} '{params.dot_coords}' '{params.imdir}' '{params.outdir}' -y &> {log}
    """

rule merge:
    version: GPSEQ_FROMFISH_MERGE_VERSION
    input:
        expand("%s/fromfish/{sample}/wCentr.out.dilate%d.{sample}_auto_userdots.tsv" % (config['output_root_folder'], config['dilation']), sample = config['datasets'])
    output:
        "%s/fromfish_merged/copies.merged.tsv" % config['output_root_folder'],
        "%s/fromfish_merged/density_profile.merged.tsv" % config['output_root_folder'],
        "%s/fromfish_merged/dots.merged.tsv" % config['output_root_folder'],
        "%s/fromfish_merged/ncomps.merged.tsv" % config['output_root_folder'],
        "%s/fromfish_merged/nuclei.merged.tsv" % config['output_root_folder'],
        "%s/fromfish_merged/volume_profile.merged.tsv" % config['output_root_folder']
    log:
        "%s/fromfish_merged/gpseq_fromfish_merge.log" % config['output_root_folder']
    params:
        meta = config['metadata'],
        indir = "%s/fromfish" % config['output_root_folder'],
        outdir = "%s/fromfish_merged" % config['output_root_folder'],
        aspect = " ".join([str(x) for x in config['aspect']]),
        sep = "-d %s" % config['metadata_delim'] if "\t" != config['metadata_delim'] else ""
    threads: 100
    shell: """
        mkdir -p {params.outdir}
        gpseq_fromfish_merge --no-date -t {threads} \
            -m {params.meta} -i {params.indir} -o {params.outdir} \
            -a {params.aspect} {params.sep} &> {log}
    """